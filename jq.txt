timew export :week |
jq -r '["id", "start", "end", "duration", "quarter_hours", "description"],
(.[] |
    # make sure .end is set (may be empty for currently active tracked time)
    .end = (.end // (now | strftime("%Y%m%dT%H%M%SZ"))) |
    .duration = ( (.end | strptime("%Y%m%dT%H%M%SZ") | mktime) - (.start | strptime("%Y%m%dT%H%M%SZ") | mktime) ) |
    # round duration to quarter hours
    .quarter_hours = (.duration / 3600 / 0.25 | ceil*0.25) |
    [
        .id,
        # urks, localtimes are a mess in jq, ymmv - as long as it is consistent off I do not care tho
        (.start | strptime("%Y%m%dT%H%M%SZ") | mktime | todateiso8601),
        (.end | strptime("%Y%m%dT%H%M%SZ") | mktime | todateiso8601),
        (.duration | strftime("%T")),
        .quarter_hours,
        (.tags | join(", "))
    ]
) |
@csv'


timew export :month |
jq -r '["id", "start", "end", "duration", "quarter_hours", "tags", "description"],
(.[] |
    # make sure .end is set (may be empty for currently active tracked time)
    .end = (.end // (now | strflocaltime("%Y%m%dT%H%M%SZ"))) |
    .duration = ( (.end | strptime("%Y%m%dT%H%M%SZ") | mktime) - (.start | strptime("%Y%m%dT%H%M%SZ") | mktime) ) |
    # round duration to quarter hours
    .quarter_hours = (.duration / 3600 / 0.25 | ceil*0.25) |
    [
        .id,
        # urks, localtimes are a mess in jq, ymmv - as long as it is consistent off I do not care tho
        (.start | strptime("%Y%m%dT%H%M%SZ") | mktime | todateiso8601),
        (.end | strptime("%Y%m%dT%H%M%SZ") | mktime | todateiso8601),
        (.duration | strflocaltime("%T")),
        .quarter_hours,
        (.tags | join(", ")),
        .annotation
    ]
) |
@csv'


https://beko.famkos.net/2021/09/10/exporting-timewarrior-summary-with-duration-and-quarter-hours/
